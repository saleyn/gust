defmodule GustPy.Parser.Adapter do
  @moduledoc false

  @behaviour Gust.DAG.Parser.Adapter
  alias Gust.DAG.Definition
  alias Gust.DAG.Graph
  alias GustPy.Executor

  @impl true
  def extension, do: ".py"

  @impl true
  def parse_file(file_path) do
    name = Path.basename(file_path, extension())

    with {out, 0} <- Executor.run(["parse", "--file", file_path]),
         [dag_json | _] <- Glazer.JSON.decode!(out),
         {:ok, dag_def} <- parse_dag_def(dag_json, name) do
      {:ok, dag_def}
    else
      {:error, :uv_not_found} ->
        error =
          {[], "`uv` executable not found on your PATH", ""}

        {:error, error}

      [] ->
        error = {[], "Not a GustPy file", ""}
        {:error, error}

      {:error, error} ->
        {:error, error}

      {_out, exit} ->
        {:error, {[line: ""], "Parse file command failed, exit: #{exit}", ""}}
    end
  end

  defp put_store_result(tasks, all_tasks) do
    for {t_name, opts} <- tasks, into: %{} do
      {t_name, Map.put(opts, :store_result, all_tasks[t_name]["save"])}
    end
  end

  defp parse_dag_def(%{"error" => error}, _name) when map_size(error) > 0 do
    {:error, {[line: error["line"]], "parsing error", error["description"]}}
  end

  defp parse_dag_def(
         %{"mod" => mod, "tasks" => tasks, "options" => opts, "file_path" => file_path},
         name
       ) do
    list = parse_downstream(tasks)

    tasks = Graph.link_tasks(list) |> put_store_result(tasks)

    stages = tasks |> Graph.to_stages() |> then(fn {:ok, stages} -> stages end)

    options = parse_options(opts)

    {:ok,
     %Definition{
       name: name,
       mod: mod,
       adapter: :python,
       task_list: List.flatten(stages),
       stages: stages,
       file_path: file_path,
       options: options,
       tasks: tasks
     }}
  end

  defp parse_downstream(tasks) do
    for {task_name, %{"downstream" => deps}} <- tasks do
      {task_name, [downstream: deps]}
    end
  end

  defp parse_options(opts) do
    Enum.flat_map(["on_finished_callback", "schedule"], fn key ->
      if Map.has_key?(opts, key) do
        [{option_key(key), Map.fetch!(opts, key)}]
      else
        []
      end
    end)
  end

  defp option_key("on_finished_callback"), do: :on_finished_callback
  defp option_key("schedule"), do: :schedule
end
